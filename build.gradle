plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.3.20'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group = 'dev.muha'
version = '1.0.0-SNAPSHOT'

repositories {
  mavenCentral()
  jcenter()
  maven { url = "https://dl.bintray.com/sdeleuze/maven/" }
  maven { url = "http://download.osgeo.org/webdav/geotools/" }
}

ext {
  kotlinVersion = '1.3.41'
  vertxVersion = '3.8.3'
  junitJupiterEngineVersion = '5.4.0'
}

application {
  mainClassName = 'io.vertx.core.Launcher'
}


def mainVerticleName = 'dev.muha.okbase.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin:$vertxVersion"
  implementation "io.vertx:vertx-mongo-client:$vertxVersion"
  implementation "io.vertx:vertx-auth-jwt:$vertxVersion"
  implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertxVersion"
  implementation "io.vertx:vertx-web-client:$vertxVersion"

  implementation "at.favre.lib:bcrypt:0.8.0"

  implementation 'com.github.ben-manes.caffeine:caffeine:2.7.0'
  implementation "com.zandero:rest.vertx:0.8.9"

  implementation "com.zaxxer:HikariCP:3.3.1"

  implementation 'org.jetbrains.exposed:exposed-core:0.18.1'
  implementation 'org.jetbrains.exposed:exposed-dao:0.18.1'
  implementation 'org.jetbrains.exposed:exposed-jdbc:0.18.1'
  implementation 'org.jetbrains.exposed:exposed-java-time:0.18.1'

  implementation "org.hibernate.validator:hibernate-validator:6.0.16.Final"
  implementation "org.hibernate:hibernate-agroal:5.4.2.Final"

  implementation 'io.github.microutils:kotlin-logging:1.6.26'
  implementation "javax.el:javax.el-api:3.0.1-b06"
  implementation "org.glassfish:javax.el:3.0.1-b09"
  implementation "org.slf4j:slf4j-simple:1.8.0-beta4"
  implementation "org.slf4j:jul-to-slf4j:1.8.0-beta4"
  implementation "org.slf4j:slf4j-api:1.8.0-beta4"

  implementation "org.postgresql:postgresql:42.2.5"
  implementation "net.postgis:postgis-jdbc:2.3.0"
  implementation "com.github.mayconbordin:postgis-geojson:1.1"

  testImplementation "io.kotlintest:kotlintest-runner-junit5:3.3.2"
  testImplementation "org.assertj:assertj-core:3.11.1"
  testImplementation "org.junit.platform:junit-platform-launcher:1.5.1"
  testImplementation "io.vertx:vertx-unit:$vertxVersion"
  testImplementation "io.vertx:vertx-junit5:$vertxVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterEngineVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterEngineVersion"
}

compileKotlin {
  kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
  kotlinOptions.jvmTarget = '1.8'
}


shadowJar {
  classifier = 'fat'
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'PASSED', 'FAILED', 'SKIPPED'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}
